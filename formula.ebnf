<expr> ::= <equality>
<equality> ::= <relational> (("=" | "<>") <relational>)*
<relational> ::= <concat> (("<" | "<=" | ">" | ">=") <concat>)*
<concat> ::= <add> ("&" <add>)*
<add> ::= <mul> (("+" | "-") <mul>)*
<mul> ::= <unary> ("*" <unary> | "/" <unary>)*
<unary> ::= ("+" | "-")? <primary>

<primary> ::= <num>
| <ident>
| <string>
| "{" <array_rows> "}"
| <funcname> "(" <args>? ")"
| "(" <expr> ")"
| <refs>

<array_rows> ::= <array_row> (";" <array_row>)*
<array_row> ::= <constant> ("," <constant>)*
<constant> ::= <num> | <string> | "TRUE" | "FALSE"
<args> ::= <expr> ("," <expr>?)*

<refs> ::= <ref> (":" <ref>)*
<ref> ::= "$"? <column_letter> "$"? <num>

<column_letter> ::= <char> <char>? <char>?
<funcname> ::= <ident> ("." | <ident> | <num>)*
<string> ::= "\"" any* "\""
<ident> ::= (<char> | <num> | "_" | "\")+
<num> ::= <digit>+

<digit> ::= [0-9]
<char> ::= ([a-z] | [A-Z])
